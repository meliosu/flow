hoare_triple = ws "{" ws condition:pre ws "}" ws program:program ws "{" ws condition:post ws "}" ws 
  { HoareTriple(:pre, :program, :post) }; 

program = ws declaration*:decls ws body:body ws { Program(:decls, :body) };

expr = add | sub | mul | div | neg | int | apply | update | array | var;

add = ws "(" ws expr:lhs ws "+" ws expr:rhs ws ")" ws { Add(:lhs, :rhs) };
sub = ws "(" ws expr:lhs ws "-" ws expr:rhs ws ")" ws { Sub(:lhs, :rhs) };
mul = ws "(" ws expr:lhs ws "*" ws expr:rhs ws ")" ws { Mul(:lhs, :rhs) };
div = ws "(" ws expr:lhs ws "/" ws expr:rhs ws ")" ws { Div(:lhs, :rhs) };
neg = ws "-" ws expr:inner ws { Neg(:inner) };
int = integer:integer { Int(:integer) };
var = name$name { Var($name) };
apply = ws "app" ws "(" ws expr:array ws "," ws expr:index ws ")" ws { Apply(:array, :index) };
update = ws "upd" ws "(" ws expr:array ws "," ws expr:index ws "," ws expr:value ws ")" ws { Update(:array, :index, :value) };
array = ws "[" ws expr:first ("," ws expr)*:other ws "]" ws { buildArray(:first, :other) };

type = int_type | array_type;

int_type = ws "int" ws  { IntType() };
array_type = ws "array of" ws type:type  ws { ArrayType(:type) };

relation = 
      "=" { Equal() }
    | "!=" { NotEqual() }
    | "<=" { LessOrEqual() }
    | "<"  { Less() }
    | ">=" { GreaterOrEqual() }
    | ">"  { Greater() };

condition = true | false | comparison | and | or | implies | not;

true = ws "true" ws { True() };
false = ws "false" ws { False() }; 
comparison = ws expr:lhs ws relation:relation ws expr:rhs ws { Comparison(:lhs, :relation, :rhs) };
and = ws "(" ws condition:lhs ws ("&&" | "∧") ws condition:rhs ws ")" ws { And(:lhs, :rhs) };
or = ws "(" ws condition:lhs ws ("||" | "∨") ws condition:rhs ws ")" ws { Or(:lhs, :rhs) };
implies = ws "(" ws condition:lhs ws "->" ws condition:rhs ws ")" ws { Implies(:lhs, :rhs) };
not = ws "!" ws "(" ws condition:condition ws ")" ws { Not(:condition) };

declaration = ws "var" ws name$name ws ":" ws type:type ws ";" ws { Declaration($name, :type) };

body = assignment | test | iteration | sequence | selection;
assignment = ws name$name ws ":=" ws expr:expr ws { Assignment($name, :expr) };
test = ws "(" ws condition:condition ws ")" ws "?" ws { Test(:condition) };
_sequence = ws body:body ws ";" ws { :body }; 
sequence = ws "{" ws _sequence*:bodies ws "}" ws { Sequence(:bodies) };
selection =  ws "{" ws body:first (ws "U" ws body)+:other ws "}" ws { buildSelection(:first, :other) };
iteration = ws "{" ws condition:inv ws "}" ws "{" _sequence*:bodies ws "}" ws "*" ws { buildIteration(:bodies, :inv) };


ws = (" " | "\t" | "\r" | "\n")*;
integer = ('0'-'9')+$integer { s2i($integer) };
name = ('a'-'z' | 'A'-'Z') ('a'-'z' | 'A'-'Z' | '0'-'9')*;